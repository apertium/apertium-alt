Alphabet
 А Б В Г Д Е Ё Ж З И И Ј К Л М Н Ҥ О Ӧ П Р С Т У Ӱ Ф Х Ц Ч Ш Щ Ь Ы Ъ Э Ю Я
 а б в г д е ё ж з и й ј к л м н ҥ о ӧ п р с т у ӱ ф х ц ч ш щ ь ы ъ э ю я
 %0 1 2 3 4 5 6 7 8 9

 %{I%}:ы
 %{i%}:ы
 %{N%}:н
 %{D%}:д
 %{A%}:а
 %{L%}:л
 %{B%}:б
 %{E%}:а
 %{z%}:з
 %{n%}:0
 %{G%}:г
 %{l%}:л
 %{Z%}:з
 
 %-
 %+
 % 
 %>:0  ;

Sets

Vow = А Е Э И Ы О У Ӧ Ӱ
      а е э и ы о у ӧ ӱ ;

Cns =  Б В Г Д Ж З Й Ј К Л М Н Ҥ П Р С Т Ф Х Ц Ч Ш Щ Ь Ъ
       б в г д ж з й ј к л м н ҥ п р с т ф х ц ч ш щ ь ъ ;


VclsCns = П Т К Х С Ш Щ Ф  Д Г Б
          п т к х с ш щ ф  д г б ;

Sonorant =  й Л М Н Ҥ Р
            й л м н ҥ р ;

VcdLowSon = Л М Н Ҥ
            л м н ҥ ;

Nasal = М Н Ҥ
        м н ҥ ;

Rules 

!!! Vowel stuff !!!

"{I} deletes after another vowel"
%{I%}:0 <=> :Vow/[ :0 ]* _ ;

"{i} deletes after another vowel"
%{i%}:0 <=> :Vow/[ :0 ]* _ ;

!"{I} front rounding harmony"
!%{I%}:ӱ <=> [ :ӱ :Cns+ ]/[ :0 ]* _ ;

"{I} front harmony"
%{I%}:и <=> [ [ :ӱ | :ӧ | :и | :э | :е ] :Cns+ ]/[ :0 ]* _ ;

"{i} front harmony"
%{i%}:и <=> [ [ :и | :э | :е ] :Cns+ ]/[ :0 ]* _ ;

"{i} back rounding harmony"
%{i%}:у <=> [ [ :о | :ё | :у | :ю ] :Cns+ ]/[ :0 ]* _ ;

"{i} front rounding harmony"
%{i%}:у <=> [ [ :ӱ | :ӧ ] :Cns+ ]/[ :0 ]* _ ;

"{A} front harmony"
%{A%}:е <=> [ [ :ӱ | :и | :э | :е ] :Cns+ ]/[ :0 ]* _ ;

"{A} rounding harmony"
%{A%}:о <=> [ :о :Cns+ ]/[ :0 ]* _ ;

"{A} front rounding harmony"
%{A%}:ӧ <=> [ :ӧ :Cns+ ]/[ :0 ]* _ ;

"{E} front harmony"
%{E%}:е <=> [ [ :ӱ | :и | :э | :е ] :Cns+ ]/[ :0 ]* _ ;

"{E} rounding harmony"
%{E%}:о <=> [ :о :Cns+ ]/[ :0 ]* _ ;

"{E} front rounding harmony"
%{E%}:ӧ <=> [ :ӧ :Cns+ ]/[ :0 ]* _ ;

"{E} as й after a vowel"
%{E%}:й <=> :Vow/[ :0 ]* _ ;



!!! Consonant stuff !!!

"{N} is realised as т after voiceless consonants"
%{N%}:т <=> :VclsCns/[ :0 ]* _ ;

"{N} is realised as д after sonorants"
%{N%}:д <=> :Sonorant/[ :0 ]* _ ;
        except
            :Nasal %>: _ [ :Vow :Nasal ]/[ :0 ]* ;  ! nasal harmony

"{D} nasal harmony"
%{D%}:н <=> :Nasal %>: _ [ :Vow :Nasal ]/[ :0 ]* ;

"{D} is realised as т after voiceless consonants"
%{D%}:т <=> :VclsCns/[ :0 - %{n%}: ]* _ ;

!@ п о д ъ е з д >:0 {D}:т {I}:и ҥ

"{G} is realised as к after voiceless consonants"
%{G%}:к <=> :VclsCns/[ :0 - %{n%}: ]* _ ;

"{L} is realised as д after voiced consonants of equal or lower sonority"
%{L%}:д <=> [ :VcdLowSon ( :ь ) ]/[ :0 ]* _ ;

!@ м о д у л ь >:0 {L}:д {A}:а р

"{L} is realised as т after voiceless consonants"
%{L%}:т <=> :VclsCns/[ :0 ]* _ ;

"Voice certain stem-final consonants when intervocalic"
VclsCoronal:VcdCoronal <=> :Vow _ [ %>: :Vow ]/[ :0 - %>: ]* ;
                       where VclsCoronal in ( с т ш п к )
                              VcdCoronal in ( з д ж б г )
                       matched ;

"{Z} is realised as с after voiceless consonants"
%{Z%}:с <=> :VclsCns/[ :0 ]* _ ;



!!! Case + possessive stuff !!!

"{z} isn't realised as z after consonants"
%{z%}:0 <=> [ :Cns (:ь) ]/[ :0 ]* _ ;

"Dative {G} deleted after px3sp"
%{G%}:0 <=> %{n%}: %>: _ ;

"{n} is н before locative"
%{n%}:н <=> _ %>: %{D%}: %{A%}: .#. ;

"{D} of ablative is н after px3sp"
%{D%}:н <=> %{n%}:0 %>: _ %{A%}: :ҥ .#. ;

!@ б а ш:ж >:0 {z}:0 {I}:ы {n}:0 >:0 {D}:н {A}:а ҥ	


! Resources:
! http://wiki.apertium.org/wiki/Starting_a_new_language_with_HFST#Enter_twol
! https://kitwiki.csc.fi/twiki/bin/view/KitWiki/HfstHome
! http://hfst.sourceforge.net/
! http://wiki.apertium.org/wiki/Twol
! http://wiki.apertium.org/wiki/Hfst

